plugins {
    id 'java'
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "2.2.21"
    id 'me.modmuss50.mod-publish-plugin' version '0.8.1'
}

version = project.mod_version
group = project.package

base {
	archivesName = project.mod_id
}

repositories {
    // ... your repositories
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"${project.mod_id}" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft": project.minecraft_version,
                "fabricloader": project.loader_version,
                "mod_id": project.mod_id,
                "mod_name": project.mod_name,
                "github_repo": project.github_repo
    }
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 21
	}
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName
    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

def static getBranch() {
    def env = System.getenv()
    if (env.GITHUB_REF) {
        def branch = env.GITHUB_REF
        return branch.substring(branch.lastIndexOf("/") + 1)
    }
    return "main"
}

publishMods {
    file = remapJar.archiveFile
    changelog = System.getenv("CHANGELOG") ?: "No changelog provided"
    type = STABLE
    displayName = "Yas Zoom ${project.version}"
    modLoaders.add("fabric")
    dryRun = System.getenv("GITHUB_TOKEN") == null

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = project.modrinth_id
        minecraftVersions.add("${project.minecraft_version}")
        projectDescription = providers.fileContents(layout.projectDirectory.file("readme.md")).asText
    }

    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = System.getenv("CI") ? providers.environmentVariable("GITHUB_REPOSITORY") : project.github_repo
        commitish = System.getenv("CI") ? getBranch() : "main"
    }
}